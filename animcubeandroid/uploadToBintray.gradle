apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        Publication(MavenPublication) {
            artifact("$buildDir/outputs/aar/animcubeandroid-release.aar")
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.catalinjurjiu'
            artifactId 'animcube-android'
            version '1.0.3'

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty('bintrayUser')
    key = properties.getProperty('bintrayApiKey')
    publications = ['Publication']
    pkg {
        repo = 'cjurjiu-opensource'
        name = 'animcube-android'
        labels = ['android', 'rubik\'s cube', 'surface view', 'animcube']
        publicDownloadNumbers = true
        override = true
        version {
            name = '1.0.3'
            desc = 'AnimCube for Android v1.0.3'
            released = new Date()
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/cjurjiu/animcubeandroid.git'
        }
    }
}